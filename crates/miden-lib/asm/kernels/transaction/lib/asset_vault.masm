use.std::collections::smt

use.kernel::account
use.kernel::asset
use.kernel::memory

# ERRORS
# =================================================================================================

const.ERR_VAULT_GET_BALANCE_PROC_CAN_ONLY_BE_CALLED_ON_FUNGIBLE_FAUCET="the get_balance procedure can only be called on a fungible faucet"

const.ERR_VAULT_HAS_NON_FUNGIBLE_ASSET_PROC_CAN_BE_CALLED_ONLY_WITH_NON_FUNGIBLE_ASSET="the has_non_fungible_asset procedure can only be called on a non-fungible faucet"

const.ERR_VAULT_FUNGIBLE_MAX_AMOUNT_EXCEEDED="adding the fungible asset to the vault would exceed the max amount of 9223372036854775807"

const.ERR_VAULT_ADD_FUNGIBLE_ASSET_FAILED_INITIAL_VALUE_INVALID="failed to add fungible asset to the asset vault due to the initial value being invalid"

const.ERR_VAULT_NON_FUNGIBLE_ASSET_ALREADY_EXISTS="the non-fungible asset already exists in the asset vault"

const.ERR_VAULT_FUNGIBLE_ASSET_AMOUNT_LESS_THAN_AMOUNT_TO_WITHDRAW="failed to remove the fungible asset from the vault since the amount of the asset in the vault is less than the amount to remove"

const.ERR_VAULT_REMOVE_FUNGIBLE_ASSET_FAILED_INITIAL_VALUE_INVALID="failed to remove fungible asset from the asset vault due to the initial value being invalid"

const.ERR_VAULT_NON_FUNGIBLE_ASSET_TO_REMOVE_NOT_FOUND="failed to remove non-existent non-fungible asset from the vault"

# CONSTANTS
# =================================================================================================

# The bitmask that when applied will set the fungible bit to zero.
const.INVERSE_FUNGIBLE_BITMASK_U32=0xffffffdf # last byte: 0b1101_1111

# ACCESSORS
# =================================================================================================

#! Returns the balance of a fungible asset associated with a faucet_id.
#!
#! Inputs:  [faucet_id_prefix, faucet_id_suffix, vault_root_ptr]
#! Outputs: [balance]
#!
#! Where:
#! - vault_root_ptr is a pointer to the memory location at which the vault root is stored.
#! - faucet_id_prefix is the prefix of the faucet id of the fungible asset of interest.
#! - balance is the vault balance of the fungible asset.
#!
#! Panics if:
#! - the asset is not a fungible asset.
export.get_balance
    # assert that the faucet id is a fungible faucet
    dup exec.account::is_fungible_faucet
    assert.err=ERR_VAULT_GET_BALANCE_PROC_CAN_ONLY_BE_CALLED_ON_FUNGIBLE_FAUCET
    # => [faucet_id_prefix, faucet_id_suffix, vault_root_ptr]

    # get the asset vault root
    padw movup.6 mem_loadw
    # => [ASSET_VAULT_ROOT, faucet_id_prefix, faucet_id_suffix]

    # prepare the key for fungible asset lookup (pad least significant elements with zeros)
    push.0.0 movup.7 movup.7
    # => [faucet_id_prefix, faucet_id_suffix, 0, 0, ASSET_VAULT_ROOT]

    # lookup asset
    exec.smt::get swapw dropw
    # => [ASSET]

    # extract asset balance (ASSET[0])
    drop drop drop
    # => [balance]
end

#! Returns a boolean indicating whether the non-fungible asset is present in the vault.
#!
#! Inputs:  [ASSET, vault_root_ptr]
#! Outputs: [has_asset]
#!
#! Where:
#! - vault_root_ptr is a pointer to the memory location at which the vault root is stored.
#! - ASSET is the non-fungible asset of interest
#! - has_asset is a boolean indicating whether the account vault has the asset of interest
#!
#! Panics if:
#! - the ASSET is a fungible asset.
export.has_non_fungible_asset
    # check if the asset is a non-fungible asset
    exec.asset::is_non_fungible_asset
    assert.err=ERR_VAULT_HAS_NON_FUNGIBLE_ASSET_PROC_CAN_BE_CALLED_ONLY_WITH_NON_FUNGIBLE_ASSET
    # => [ASSET, vault_root_ptr]

    # build the asset key from the non-fungible asset
    exec.build_non_fungible_asset_vault_key
    # => [ASSET_KEY, vault_root_ptr]

    # prepare the stack to read non-fungible asset from vault
    padw movup.8 mem_loadw swapw
    # => [ASSET_KEY, ACCT_VAULT_ROOT]

    # lookup asset
    exec.smt::get swapw dropw
    # => [ASSET]

    # compare with EMPTY_WORD to assess if the asset exists in the vault
    padw eqw not
    # => [has_asset, PAD, ASSET]

    # organize the stack for return
    movdn.4 dropw movdn.4 dropw
    # => [has_asset]
end

# ADD ASSET
# =================================================================================================

#! Add the specified fungible asset to the vault. If the vault already contains an asset issued by
#! the same faucet, the amounts are added together.
#!
#! Inputs:  [ASSET, vault_root_ptr]
#! Outputs: [ASSET']
#!
#! Where:
#! - vault_root_ptr is a pointer to the memory location at which the vault root is stored.
#! - ASSET is the fungible asset to add to the vault.
#! - ASSET' is the total fungible asset in the account vault after ASSET was added to it.
#!
#! Panics if:
#! - the total value of assets is greater than or equal to 2^63.
export.add_fungible_asset
    # Create the asset key from the asset.
    # ---------------------------------------------------------------------------------------------

    # => [faucet_id_prefix, faucet_id_suffix, 0, amount, vault_root_ptr]
    dupw
    # => [faucet_id_prefix, faucet_id_suffix, 0, amount, faucet_id_prefix, faucet_id_suffix, 0, amount, vault_root_ptr]
    push.0 swap.4 drop
    # => [[faucet_id_prefix, faucet_id_suffix, 0, 0], faucet_id_prefix, faucet_id_suffix, 0, amount, vault_root_ptr]
    movup.6 drop
    # => [[faucet_id_prefix, faucet_id_suffix, 0, 0], faucet_id_prefix, faucet_id_suffix, amount, vault_root_ptr]
    padw dup.11
    # => [vault_root_ptr, pad(4), ASSET_KEY, faucet_id_prefix, faucet_id_suffix, amount, vault_root_ptr]

    # Get the asset vault root and read the current asset using the `push_smtpeek` decorator.
    # ---------------------------------------------------------------------------------------------

    # the current asset may be the empty word if it does not exist and so its faucet id would be zeroes
    # we therefore overwrite the faucet id with the faucet id from ASSET to account for this edge case
    mem_loadw swapw
    # => [ASSET_KEY, VAULT_ROOT, faucet_id_prefix, faucet_id_suffix, amount, vault_root_ptr]
    adv.push_smtpeek
    adv_loadw
    # => [CUR_VAULT_VALUE, VAULT_ROOT, faucet_id_prefix, faucet_id_suffix, amount, vault_root_ptr]
    swapw
    # => [VAULT_ROOT, CUR_VAULT_VALUE, faucet_id_prefix, faucet_id_suffix, amount, vault_root_ptr]
    dupw.1
    # => [CUR_VAULT_VALUE, VAULT_ROOT, CUR_VAULT_VALUE, faucet_id_prefix, faucet_id_suffix, amount, vault_root_ptr]
    drop drop
    # => [[0, cur_amount], VAULT_ROOT, CUR_VAULT_VALUE, faucet_id_prefix, faucet_id_suffix, amount, vault_root_ptr]
    movup.11 movup.11
    # => [[faucet_id_prefix, faucet_id_suffix, 0, cur_amount], VAULT_ROOT, CUR_VAULT_VALUE, amount, vault_root_ptr]

    # Check the new amount does not exceed the maximum allowed amount and add the two
    # fungible assets together.
    # ---------------------------------------------------------------------------------------------

    # arrange amounts
    movup.3 movup.12 dup
    # => [amount, amount, cur_amount, faucet_id_prefix, faucet_id_suffix, 0, VAULT_ROOT, CUR_VAULT_VALUE, vault_root_ptr]

    # compute max_amount - cur_amount
    exec.asset::get_fungible_asset_max_amount dup.3 sub
    # => [(max_amount - cur_amount), amount, amount, cur_amount, faucet_id_prefix, faucet_id_suffix, 0, VAULT_ROOT,
    #     CUR_VAULT_VALUE, vault_root_ptr]

    # assert amount + cur_amount < max_amount
    lte assert.err=ERR_VAULT_FUNGIBLE_MAX_AMOUNT_EXCEEDED
    # => [amount, cur_amount, faucet_id_prefix, faucet_id_suffix, 0, VAULT_ROOT, CUR_VAULT_VALUE, vault_root_ptr]

    # add asset amounts
    add movdn.3
    # => [ASSET', VAULT_ROOT, CUR_VAULT_VALUE, vault_root_ptr]

    # Create the asset key and insert the updated asset.
    # ---------------------------------------------------------------------------------------------

    # create the asset key to prepare insertion of the asset into the vault
    dupw movdnw.3
    # => [ASSET', VAULT_ROOT, CUR_VAULT_VALUE, ASSET', vault_root_ptr]
    dupw
    # => [ASSET', ASSET', VAULT_ROOT, CUR_VAULT_VALUE, ASSET', vault_root_ptr]
    push.0 swap.4 drop
    # => [[faucet_id_prefix, faucet_id_suffix, 0, 0], ASSET', VAULT_ROOT, CUR_VAULT_VALUE, ASSET', vault_root_ptr]
    swapw
    # => [ASSET', ASSET_KEY', VAULT_ROOT, CUR_VAULT_VALUE, ASSET', vault_root_ptr]

    # update asset in vault and assert the old value is equivalent to the value provided via the
    # decorator
    exec.smt::set
    # => [PREV_ASSET, VAULT_ROOT', CUR_VAULT_VALUE, ASSET', vault_root_ptr]
    movupw.2 assert_eqw.err=ERR_VAULT_ADD_FUNGIBLE_ASSET_FAILED_INITIAL_VALUE_INVALID
    # => [VAULT_ROOT', ASSET', vault_root_ptr]

    # update the vault root
    movup.8 mem_storew dropw
    # => [ASSET']
end

#! Add the specified non-fungible asset to the vault.
#!
#! Inputs:  [ASSET, vault_root_ptr]
#! Outputs: [ASSET]
#!
#! Where:
#! - vault_root_ptr is a pointer to the memory location at which the vault root is stored.
#! - ASSET is the non-fungible asset that is added to the vault.
#!
#! Panics if:
#! - the vault already contains the same non-fungible asset.
export.add_non_fungible_asset
    # Build the asset key from the non-fungible asset.
    # ---------------------------------------------------------------------------------------------

    dupw exec.build_non_fungible_asset_vault_key
    # => [ASSET_KEY, ASSET, vault_root_ptr]

    # Load VAULT_ROOT and insert asset.
    # ---------------------------------------------------------------------------------------------

    padw dup.12
    # => [vault_root_ptr, pad(4), ASSET_KEY, ASSET, vault_root_ptr]
    mem_loadw swapw
    # => [ASSET_KEY, VAULT_ROOT, ASSET, vault_root_ptr]
    dupw.2
    # => [ASSET, ASSET_KEY, VAULT_ROOT, ASSET, vault_root_ptr]

    # insert asset into vault
    exec.smt::set
    # => [OLD_VAL, VAULT_ROOT', ASSET, vault_root_ptr]

    # assert old value was empty
    padw assert_eqw.err=ERR_VAULT_NON_FUNGIBLE_ASSET_ALREADY_EXISTS
    # => [VAULT_ROOT', ASSET, vault_root_ptr]

    # update the vault root
    movup.8 mem_storew dropw
    # => [ASSET]
end

#! Add the specified asset to the vault.
#!
#! Inputs:  [ASSET, vault_root_ptr]
#! Outputs: [ASSET']
#!
#! Where:
#! - ASSET is the asset that is added to the vault.
#! - vault_root_ptr is a pointer to the memory location at which the vault root is stored.
#! - ASSET' final asset in the account vault defined as follows:
#!   - If ASSET is a non-fungible asset, then ASSET' is the same as ASSET.
#!   - If ASSET is a fungible asset, then ASSET' is the total fungible asset in the account vault
#!     after ASSET was added to it.
#!
#! Panics if:
#! - the asset is not valid.
#! - the total value of two fungible assets is greater than or equal to 2^63.
#! - the vault already contains the same non-fungible asset.
export.add_asset
    # check if the asset is a fungible asset
    exec.asset::is_fungible_asset
    # => [is_fungible_asset, ASSET]

    # add the asset to the asset vault
    if.true
        # validate the fungible asset
        exec.asset::validate_fungible_asset
        # => [ASSET]

        exec.add_fungible_asset
        # => [ASSET']
    else
        # validate the non-fungible asset
        exec.asset::validate_non_fungible_asset
        # => [ASSET]

        exec.add_non_fungible_asset
        # => [ASSET']
    end
end

# REMOVE ASSET
# =================================================================================================

#! Remove the specified fungible asset from the vault.
#!
#! Inputs:  [ASSET, vault_root_ptr]
#! Outputs: [ASSET]
#!
#! Where:
#! - ASSET is the fungible asset to remove from the vault.
#! - vault_root_ptr is a pointer to the memory location at which the vault root is stored.
#!
#! Panics if:
#! - the amount of the asset in the vault is less than the amount to be removed.
export.remove_fungible_asset
    dupw push.0 movdn.3 dup movdn.4
    # => [ASSET_KEY, faucet_id, amount, ASSET, vault_root_ptr]

    # get the asset vault root and read the vault asset value using the `push_smtpeek` decorator
    # To account for the edge case in which CUR_VAULT_VALUE is an EMPTY_WORD, we replace the most
    # significant element with the faucet_id to construct the CUR_ASSET.
    padw dup.14 mem_loadw swapw
    adv.push_smtpeek
    adv_loadw dupw movdnw.2 drop movup.11
    # => [CUR_ASSET, VAULT_ROOT, CUR_VAULT_VALUE, amount, ASSET, vault_root_ptr]

    # arrange elements
    movup.3 movup.12 dup dup.2
    # => [cur_amount, amount, amount, cur_amount, faucet_id, 0, 0, VAULT_ROOT, CUR_VAULT_VALUE,
    #     ASSET, vault_root_ptr]

    # assert amount <= cur_amount
    lte assert.err=ERR_VAULT_FUNGIBLE_ASSET_AMOUNT_LESS_THAN_AMOUNT_TO_WITHDRAW
    # => [amount, cur_amount, faucet_id, 0, 0, VAULT_ROOT, CUR_VAULT_VALUE, ASSET, vault_root_ptr]

    # asset amount + cur_amount < max_amount
    sub
    # => [new_amount, faucet_id, 0, 0, VAULT_ROOT, CUR_VAULT_VALUE, ASSET, vault_root_ptr]

    # => check if the asset amount is zero
    dup eq.0
    # => [is_zero, new_amount, faucet_id, 0, 0, VAULT_ROOT, CUR_VAULT_VALUE, ASSET, vault_root_ptr]

    if.true
        # fungible asset empty - insert EMPTY_WORD in vault
        movdn.3 padw
        # => [EMPTY_WORD, ASSET_KEY, VAULT_ROOT, CUR_VAULT_VALUE, ASSET, vault_root_ptr]
    else
        # fungible asset not empty - update asset in vault
        movdn.3 dupw movup.3 drop push.0 movdn.3 swapw
        # => [NEW_ASSET, ASSET_KEY, VAULT_ROOT, CUR_VAULT_VALUE, ASSET, vault_root_ptr]
    end

    # update asset in vault and assert the old value is equivalent to the value provided via the
    # decorator
    exec.smt::set movupw.2
    assert_eqw.err=ERR_VAULT_REMOVE_FUNGIBLE_ASSET_FAILED_INITIAL_VALUE_INVALID
    # => [VAULT_ROOT', ASSET, vault_root_ptr]

    # update the vault root
    movup.8 mem_storew dropw
    # => [ASSET]
end

#! Remove the specified non-fungible asset from the vault.
#!
#! Inputs:  [ASSET, vault_root_ptr]
#! Outputs: [ASSET]
#!
#! Where:
#! - ASSET is the non-fungible asset to remove from the vault.
#! - vault_root_ptr is a pointer to the memory location at which the vault root is stored.
#!
#! Panics if:
#! - the non-fungible asset is not found in the vault.
export.remove_non_fungible_asset
    # build non-fungible asset key
    dupw exec.build_non_fungible_asset_vault_key padw
    # => [pad(4), ASSET_KEY, ASSET, vault_root_ptr]

    # load vault root
    dup.12 mem_loadw
    # => [VAULT_ROOT, ASSET_KEY, ASSET, vault_root_ptr]

    # prepare insertion of an EMPTY_WORD into the vault at the asset key to remove the asset
    swapw padw
    # => [EMPTY_WORD, ASSET_KEY, VAULT_ROOT, ASSET, vault_root_ptr]

    # update asset in vault
    exec.smt::set
    # => [OLD_VAL, VAULT_ROOT', ASSET, vault_root_ptr]

    # assert old value was not empty (we only need to check ASSET[3] which is the faucet id)
    eq.0 assertz.err=ERR_VAULT_NON_FUNGIBLE_ASSET_TO_REMOVE_NOT_FOUND drop drop drop
    # => [VAULT_ROOT', ASSET, vault_root_ptr]

    # update the vault root
    movup.8 mem_storew dropw
    # => [ASSET]
end

#! Remove the specified asset from the vault.
#!
#! Inputs:  [ASSET, vault_root_ptr]
#! Outputs: [ASSET]
#!
#! Where:
#! - ASSET is the asset to remove from the vault.
#! - vault_root_ptr is a pointer to the memory location at which the vault root is stored.
#!
#! Panics if:
#! - the fungible asset is not found in the vault.
#! - the amount of the fungible asset in the vault is less than the amount to be removed.
#! - the non-fungible asset is not found in the vault.
export.remove_asset
    # check if the asset is a fungible asset
    exec.asset::is_fungible_asset
    # => [is_fungible_asset, ASSET, vault_root_ptr]

    # remove the asset from the asset vault
    if.true
        exec.remove_fungible_asset
        # => [ASSET]
    else
        exec.remove_non_fungible_asset
        # => [ASSET]
    end
end

# HELPER PROCEDURES
# =================================================================================================

#! Builds the vault key of a non fungible asset. The asset is NOT validated and therefore must
#! be a valid non-fungible asset.
#!
#! Inputs:  [ASSET]
#! Outputs: [ASSET_KEY]
#!
#! Where:
#! - ASSET is the non-fungible asset for which the vault key is built.
#! - ASSET_KEY is the vault key of the non-fungible asset.
export.build_non_fungible_asset_vault_key
    # create the asset key from the non-fungible asset by swapping hash0 with the faucet id
    # => [faucet_id_prefix, hash2, hash1, hash0]
    swap.3
    # => [hash0, hash2, hash1 faucet_id_prefix]

    # disassemble hash0 into u32 limbs
    u32split swap
    # => [hash0_lo, hash0_hi, hash2, hash1 faucet_id_prefix]

    # set the fungible bit to 0
    u32and.INVERSE_FUNGIBLE_BITMASK_U32
    # => [hash0_lo', hash0_hi, hash2, hash1 faucet_id_prefix]

    # reassemble hash0 felt by multiplying the high part with 2^32 and adding the lo part
    swap push.0x0100000000 mul add
    # => [ASSET_KEY]
end
