use.miden::account

#Â ERRORS
# =================================================================================================

const.ERR_FUNGIBLE_ASSET_PROVIDED_FAUCET_ID_IS_INVALID="failed to build the fungible asset because the provided faucet id is not from a fungible faucet"

const.ERR_FUNGIBLE_ASSET_AMOUNT_EXCEEDS_MAX_ALLOWED_AMOUNT="fungible asset build operation called with amount that exceeds the maximum allowed asset amount"

const.ERR_NON_FUNGIBLE_ASSET_PROVIDED_FAUCET_ID_IS_INVALID="failed to build the non-fungible asset because the provided faucet id is not from a non-fungible faucet"

# PROCEDURES
# =================================================================================================

#! Builds a fungible asset for the specified fungible faucet and amount.
#!
#! Inputs:  [faucet_id_prefix, faucet_id_suffix, amount]
#! Outputs: [ASSET]
#!
#! Where:
#! - faucet_id_{prefix,suffix} are the prefix and suffix felts of the faucet to create the asset
#!   for.
#! - amount is the amount of the asset to create.
#! - ASSET is the built fungible asset.
#!
#! Annotation hint: is not used anywhere except this file
export.build_fungible_asset
    # assert the faucet is a fungible faucet
    dup exec.account::is_fungible_faucet assert.err=ERR_FUNGIBLE_ASSET_PROVIDED_FAUCET_ID_IS_INVALID
    # => [faucet_id_prefix, faucet_id_suffix, amount]

    # assert the amount is valid
    dup.2 exec.get_fungible_asset_max_amount lte
    assert.err=ERR_FUNGIBLE_ASSET_AMOUNT_EXCEEDS_MAX_ALLOWED_AMOUNT
    # => [faucet_id_prefix, faucet_id_suffix, amount]

    # create the asset
    push.0 movdn.2
    # => [ASSET]
end

#! Creates a fungible asset for the faucet the transaction is being executed against.
#!
#! Inputs:  [amount]
#! Outputs: [ASSET]
#!
#! Where:
#! - amount is the amount of the asset to create.
#! - ASSET is the created fungible asset.
#!
#! Invocation: exec
export.create_fungible_asset
    # fetch the id of the faucet the transaction is being executed against.
    exec.account::get_id
    # => [id_prefix, id_suffix, amount]

    # build the fungible asset
    exec.build_fungible_asset
    # => [ASSET]
end

#! Builds a non fungible asset for the specified non-fungible faucet and amount.
#!
#! Inputs:  [faucet_id_prefix, DATA_HASH]
#! Outputs: [ASSET]
#!
#! Where:
#! - faucet_id_{prefix,suffix} are the prefix and suffix felts of the faucet to create the asset
#!   for.
#! - DATA_HASH is the data hash of the non-fungible asset to build.
#! - ASSET is the built non-fungible asset.
#!
#! Annotation hint: is not used anywhere except this file
export.build_non_fungible_asset
    # assert the faucet is a non-fungible faucet
    dup exec.account::is_non_fungible_faucet
    assert.err=ERR_NON_FUNGIBLE_ASSET_PROVIDED_FAUCET_ID_IS_INVALID
    # => [faucet_id_prefix, hash3, hash2, hash1, hash0]

    # build the asset
    swap drop
    # => [faucet_id_prefix, hash2, hash1, hash0]
    # => [ASSET]
end

#! Creates a non-fungible asset for the faucet the transaction is being executed against.
#!
#! Inputs:  [DATA_HASH]
#! Outputs: [ASSET]
#!
#! Where:
#! - DATA_HASH is the data hash of the non-fungible asset to create.
#! - ASSET is the created non-fungible asset.
#!
#! Invocation: exec
export.create_non_fungible_asset
    # get the id of the faucet the transaction is being executed against
    exec.account::get_id swap drop
    # => [faucet_id_prefix, DATA_HASH]

    # build the non-fungible asset
    exec.build_non_fungible_asset
    # => [ASSET]
end

#! Returns the maximum amount of a fungible asset.
#!
#! Stack: []
#! Outputs: [fungible_asset_max_amount]
#!
#! fungible_asset_max_amount is the maximum amount of a fungible asset.
export.::miden::util::asset::get_fungible_asset_max_amount
